define

### Attributes ###

name sub attribute,
    value string;
    
email sub attribute,
    value string;

school-year sub attribute,
    value string;

graduate-year sub attribute,
    value long;

title sub attribute,
    value string;

level sub attribute,
    value string;

rank sub attribute,
    value long;

score sub attribute,
    value long;

id sub attribute,
    value string;

grade sub attribute,
    value long;
    

### Entities ###

person sub entity, 
    abstract,
    owns id @key,
    owns name,
    owns email;
  
student sub person,
    owns graduate-year,
    plays membership:member,
    plays participation:participant,
    plays prize-winning:winner;
    
teacher sub person,
    plays teaching:teacher;

group sub entity,
    owns id @key,
    owns title,
    owns school-year,
    plays membership:group,
    plays teaching:group;

olympiad sub entity,
    owns school-year,
    owns title,
    owns level,
    plays participation:olympiad,
    plays prize-winning:olympiad;

result sub entity,
    owns title @key,
    owns rank,
    plays participation:result;


### Relations ###

membership sub relation,
    relates member,
    relates group;
    
teaching sub relation,
    relates teacher,
    relates group;

participation sub relation,
    owns score,
    owns grade,
    relates participant,
    relates olympiad,
    relates result,
    plays prize-winning:participation;

prize-winning sub relation,
    owns grade,
    relates winner,
    relates olympiad,
    relates participation;


### Rules ###

rule define-prize-winners: when {
    $p(participant: $s, olympiad: $o, result: $r) isa participation;
    $r has $rank;
    $rank isa rank;
    $rank > 0;
    $p has grade $grade;
} then {
    (winner: $s, olympiad: $o, participation: $p) isa prize-winning;
};

